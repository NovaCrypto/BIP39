/*
 *  BIP39 library, a Java implementation of BIP39
 *  Copyright (C) 2017 Alan Evans, NovaCrypto
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 *  Original source: https://github.com/NovaCrypto/BIP39
 *  You can contact the authors via github issues.
 */

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}
apply plugin: 'maven-publish'

group 'io.github.novacrypto'
version '0.1.2'

apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    maven {
        url 'https://dl.bintray.com/novacrypto/General'
    }
    jcenter()
    mavenCentral()
}

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

dependencies {
    compile 'io.github.novacrypto:ToRuntime:0.9.0@jar'
    testCompile 'junit:junit:4.11'
    testCompile 'com.google.code.gson:gson:2.7'
    testCompile 'org.assertj:assertj-core:3.8.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId group
            artifactId 'BIP39'
            version project.version
            artifact("$buildDir/libs/bip39-${project.version}-sources.jar"){
                classifier = 'sources'
            }
            artifact("$buildDir/libs/bip39-${project.version}-javadoc.jar"){
                classifier = 'javadoc'
            }
            artifact("$buildDir/libs/bip39-${project.version}.jar")
        }
    }
}

// To upload a new version
// ./gradlew clean
// ./gradlew build bintrayUpload -Puser=user -Pkey=APIKEY
bintray {
    user = project.ext.properties.user
    key = project.ext.properties.key
    publications = ['MyPublication']
    pkg {
        repo = 'BIP'
        name = 'BIP39'
        userOrg = 'novacrypto'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/NovaCrypto/BIP39.git'
        version project.version
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport